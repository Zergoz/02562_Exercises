<!DOCTYPE html>
<html>
<head>
    <title>w2p5</title>
    <script type="text/javascript" src="w2p5.js"></script>
    <script id="wgsl" type="x-shader">
    struct Uniforms {
        aspect: f32,
        camera_constant: f32,
        sphereMat: f32,
        otherMat: f32
    };
    @group(0) @binding(0) var<uniform> uniforms : Uniforms;
    
    struct VSOut 
    {
        @builtin(position) position: vec4f,
        @location(0) coords : vec2f
    };

    @vertex
    fn main_vs(@builtin(vertex_index) VertexIndex : u32) -> VSOut
    {
        const pos = array<vec2f, 4>(vec2f(-1.0, 1.0), vec2f(-1.0, -1.0), vec2f(1.0, 1.0), vec2f(1.0, -1.0));
        var vsOut: VSOut;
        vsOut.position = vec4f(pos[VertexIndex], 0.0, 1.0);
        vsOut.coords = pos[VertexIndex];
        return vsOut;
    }
    
    const Pi = 3.1415926535; // Pi, but not
    const lightPosition = vec3f(0.0, 1.0, 0.0);
    const epsilon = 0.0001;

    struct Ray 
    {
        origin: vec3f,
        direction: vec3f,
        tmin: f32,
        tmax: f32
    };
        
    fn get_camera_ray(uv: vec2f) -> Ray
    {
        const e = vec3f(2.0, 1.5, 2.0);
        const p = vec3f(0.0, 0.5, 0.0);
        const u = vec3f(0.0, 1.0, 0.0);
        var d = uniforms.camera_constant;
        var v = normalize(p-e);
        var b1 = normalize(cross(v, u));
        var b2 = cross(b1, v);
        var q = b1 * uv.x + b2 * uv.y + v * d;
        var omega = normalize(q);
        
        var ray = Ray(e, omega, 0.0, 1.0e32);
        return ray;
    }
    
    struct Light
    {
        L_i: vec3f,
        direction: vec3f,
        dist: f32
    };

    fn sample_point_light(pos: vec3f) -> Light
    {
        const lightIntensity = vec3f(Pi, Pi, Pi); 
        
        var L_i = lightIntensity / dot((lightPosition - pos), (lightPosition - pos));

        var l = lightPosition - pos;

        var dist = length(l);
        
        var light = Light(L_i, normalize(l), dist);
        return light;
    }

    struct HitInfo
    {
        has_hit: bool,
        dist: f32,
        position: vec3f,
        normal: vec3f,
        ambientColor: vec3f,
        diffuseColor: vec3f,
        shader: f32,
        ior1_over_ior2: f32,
        rho_s: f32,
        s: f32
    };

    fn intersect_scene(r: ptr<function, Ray>, hit : ptr<function, HitInfo>) -> bool
    {
        // Define scene data as constants.
        // Plane
        const planePosition = vec3f(0, 0, 0);
        const planeNormal = vec3f(0, 1, 0);
        const planeRGB = vec3f(0.1, 0.7, 0);
        
        // Triangle
        const v0 = vec3f(-0.2, 0.1, 0.9);
        const v1 = vec3f(0.2, 0.1, 0.9);
        const v2 = vec3f(-0.2, 0.1, -0.1);
        const triangleRGB = vec3f(0.4, 0.3, 0.2);

        // Sphere
        const sphereCenter = vec3f(0.0, 0.5, 0.0);
        const sphereRadius = 0.3;
        const sphereRGB = vec3f(0.0, 0.0, 0.0);
        const sphereRI = 1.5;
        const sphereShininess = 42;
        const specularReflectance = 0.1;

        // Call an intersection function for each object.
        if (intersect_plane(*r, hit, planePosition, planeNormal)) {
            (*r).tmax = min((*hit).dist, (*r).tmax);
            (*hit).has_hit = true;
            (*hit).shader = uniforms.otherMat;
            if ((*r).tmax == (*hit).dist) {
                (*hit).diffuseColor = 0.9 * planeRGB;
                (*hit).ambientColor = 0.1 * planeRGB;
            }
        }
        if (intersect_triangle(*r, hit, array<vec3f, 3>(v0, v1, v2))) {
            (*r).tmax = min((*hit).dist, (*r).tmax);
            (*hit).normal = planeNormal;
            (*hit).has_hit = true;
            (*hit).shader = uniforms.otherMat;
            if ((*r).tmax == (*hit).dist) {
                (*hit).diffuseColor = 0.9 * triangleRGB;
                (*hit).ambientColor = 0.1 * triangleRGB;
            }
        }
        if (intersect_sphere(*r, hit, sphereCenter, sphereRadius)) {
            (*r).tmax = min((*hit).dist, (*r).tmax);
            (*hit).has_hit = true;
            (*hit).shader = uniforms.sphereMat;
            (*hit).ior1_over_ior2 = sphereRI;
            (*hit).rho_s = specularReflectance;
            (*hit).s = sphereShininess;
            if ((*r).tmax == (*hit).dist) {
                (*hit).diffuseColor = 0.9 * sphereRGB;
                (*hit).ambientColor = 0.1 * sphereRGB;
            }
        }

        // For each intersection found, update (*r).tmax and store additional info about the hit.


        return (*hit).has_hit;
    }

    fn intersect_plane(r: Ray, hit: ptr<function, HitInfo>, position: vec3f, normal: vec3f) -> bool 
    {
        var t = (dot(position - r.origin, normal)) / dot(r.direction, normal);
        
        var objectHit = (t < r.tmax) & (t > r.tmin);
        if (objectHit) {
            if ((*hit).dist > 0) {
                (*hit).dist = min((*hit).dist, t);
            }
            else {
                (*hit).dist = t;
            }
            (*hit).position = r.origin + t * r.direction;
            (*hit).normal = normal;
        }
        return objectHit;
    }
    
    fn intersect_triangle(r: Ray, hit: ptr<function, HitInfo>, v: array<vec3f, 3>) -> bool 
    {
        var e0 = v[1] - v[0];
        var e1 = v[2] - v[0];
        var n = cross(e0, e1);

        var t = (dot((v[0] - r.origin), n)) / dot(r.direction, n);

        var objectHit = false;
        if ((t < r.tmax) & (t > r.tmin)) {
            var beta = dot(cross((v[0]-r.origin), r.direction), e1)/dot(r.direction, n);
            var gamma = -dot(cross((v[0]-r.origin), r.direction), e0)/dot(r.direction, n);
            objectHit = (beta >= 0) & (gamma >= 0) & ((beta + gamma) <= 1);
        }
        
        if (objectHit) {
            if ((*hit).dist > 0) {
                (*hit).dist = min((*hit).dist, t);
            }
            else {
                (*hit).dist = t;
            }
            (*hit).position = r.origin + t * r.direction;
            (*hit).normal = n;
        }
        return objectHit;
        
    }

    fn intersect_sphere(r: Ray, hit: ptr<function, HitInfo>, center: vec3f, radius: f32) -> bool 
    {
        var e = r.origin;
        var d = r.direction;
        var disc = (dot(d, (e - center)) * dot(d, (e - center))) - dot(d, d) * (dot((e - center), (e - center)) - (radius*radius));
        var objectHit = (disc >= 0);
        if (objectHit) {
            var t1 = dot(-d, (e - center)) - sqrt(disc) / dot(d, d);
            var t2 = dot(-d, (e - center)) + sqrt(disc) / dot(d, d);
            if ((t1 < r.tmax) && (t1 > r.tmin)) {
                if ((*hit).dist > 0) {
                    (*hit).dist = min((*hit).dist, t1);
                }
                else {
                    (*hit).dist = t1;
                }
                (*hit).position = r.origin + (*hit).dist * r.direction;
                (*hit).normal = normalize((*hit).position - center);
            }
            else if ((t2 < r.tmax) && (t2 > r.tmin)) {
                if ((*hit).dist > 0) {
                    (*hit).dist = min((*hit).dist, t2);
                }
                else {
                    (*hit).dist = t2;
                }
                (*hit).position = r.origin + (*hit).dist * r.direction;
                (*hit).normal = normalize((*hit).position - center);
            }
            else {
                objectHit = false;
            }
        }
        return objectHit;
    }

    fn lambertian(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
    {
        var light = sample_point_light((*hit).position);

        var ray = Ray(lightPosition, -light.direction, 0.0, light.dist - epsilon);
        var temp = HitInfo(false, 0.0, vec3f(0.0), vec3f(0.0), vec3f(0.0), vec3f(0.0), 0, 0, 0, 0);

        if (intersect_scene(&ray, &temp)) { return vec3f(0,0,0); }
        else { return ((*hit).diffuseColor/Pi) * light.L_i * (max(0, dot((*hit).normal, light.direction))); }
        
    }

    fn phong(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
    {
        var light = sample_point_light((*hit).position);
        var Lr = ((*hit).diffuseColor/Pi + (*hit).rho_s * ((*hit).s + 2)/(2*Pi) * pow(max(0, dot(-(*r).direction, reflect(-light.direction, (*hit).normal))), (*hit).s)) * light.L_i * max(0, dot(light.direction, (*hit).normal));
        
        return Lr;
    }

    fn mirror(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f 
    {   
        (*r).direction = reflect((*r).direction, (*hit).normal);
        (*r).origin = (*hit).position;
        (*r).tmin = epsilon;
        (*r).tmax = 1e32;

        (*hit).has_hit = false;
        
        return vec3f(0,0,0);
    }
    
    fn refraction(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
        {
        if (dot((*r).direction, (*hit).normal) < 0) {
            (*hit).ior1_over_ior2 = 1/(*hit).ior1_over_ior2;
        }
        else {
            (*hit).ior1_over_ior2 = (*hit).ior1_over_ior2/1;
            (*hit).normal = -(*hit).normal;
        }

        var cosSqTheta_t = 1 - pow((*hit).ior1_over_ior2, 2) * (1 - pow(dot((*r).direction, (*hit).normal),2));
        
        if (cosSqTheta_t < 0) {
            return mirror(r, hit);
        }
        else {
            (*r).direction = (*hit).ior1_over_ior2 * (dot(-(*r).direction, (*hit).normal) * (*hit).normal + (*r).direction) - (*hit).normal * sqrt(cosSqTheta_t);
            (*hit).has_hit = false;
            (*r).origin = (*hit).position;
            (*r).tmin = epsilon;
            (*r).tmax = 1e32;
            return vec3f(0,0,0);
        }
    }

    fn glossy(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
    {
        return phong(r, hit) + refraction(r, hit);
    }

    fn shade(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
    {
        switch i32((*hit).shader) {
            case 1 { return (lambertian(r, hit) + (*hit).ambientColor); }
            case 2 { return phong(r, hit); }
            case 3 { return mirror(r, hit); }
            case 4 { return refraction(r, hit); }
            case 5 { return glossy(r, hit); }
            case default { return (*hit).ambientColor + (*hit).diffuseColor; }
        }
    }

    @fragment
    fn main_fs(@location(0) coords: vec2f) -> @location(0) vec4f
    {
        const bgcolor = vec4f(0.1, 0.3, 0.6, 1.0);
        const max_depth = 10;
        let uv = vec2f(coords.x*uniforms.aspect*0.5f, coords.y*0.5f);
        var r = get_camera_ray(uv);
        var result = vec3f(0.0);
        var hit = HitInfo(false, 0.0, vec3f(0.0), vec3f(0.0), vec3f(0.0), vec3f(0.0), 0, 0, 0, 0);
        for(var i = 0; i < max_depth; i++) 
        {
            if(intersect_scene(&r, &hit)) { result += shade(&r, &hit); }
            else { result += bgcolor.rgb; break; }
            if(hit.has_hit) { break; }
        }
        return vec4f(pow(result, vec3f(1.0)), bgcolor.a);
    }
    </script>
</head>
<body>
    <canvas id="webgpu-canvas" width="512" height="512">
        Please use a browser that supports HTML5 canvas.
    </canvas>
    <select id="sphereMenu", size="6">
        <option value="0">Base Color</option>
        <option value="1">Lambertian</option>
        <option value="2">Phong</option>
        <option value="3" selected>Mirror</option>
        <option value="4">Refractive</option>
        <option value="5">Glossy</option>
    </select>
    <select id="otherMenu", size="2">
        <option value="0">Base Color</option>
        <option value="1" selected>Lambertian</option>
    </select>
</body>
</html>