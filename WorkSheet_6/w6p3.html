<!DOCTYPE html>
<html>
<head>
    <title>w6p3</title>
    <script type="text/javascript" src="w6p3.js"></script>
    <script type="text/javascript" src="../common/OBJParser_interleaved.js"></script>
    <script type="text/javascript" src="../common/BspTree_interleaved.js"></script>
    <script type="text/javascript" src="../common/MV.js"></script>
    <script type="text/javascript" src="../common/Aabb.js"></script>
    <script id="wgsl" type="x-shader">
    struct Uniforms {
        aspect: f32,
        camera_constant: f32,
        jitterSub: f32,
    };
    struct Aabb {
        min: vec3f,
        max: vec3f,
    };
    struct vAttribute {
        triangle: vec3f,
        normal: vec3f,
    }
    @group(0) @binding(0) var<uniform> uniforms : Uniforms;
    @group(0) @binding(1) var<storage> jitter: array<vec2f>; 
    @group(0) @binding(2) var<storage> attribs: array<vAttribute>; 
    @group(0) @binding(3) var<storage> faceMat: array<vec4u>;
    @group(0) @binding(4) var<uniform> aabb: Aabb;
    @group(0) @binding(5) var<storage> treeIds: array<u32>;
    @group(0) @binding(6) var<storage> bspTree: array<vec4u>;
    @group(0) @binding(7) var<storage> bspPlanes: array<f32>;
    @group(0) @binding(8) var<storage> mEmissionColor: array<vec4f>;
    @group(0) @binding(9) var<storage> light_index: array<u32>;
    
    struct VSOut
    {
        @builtin(position) position: vec4f,
        @location(0) coords : vec2f
    };

    @vertex
    fn main_vs(@builtin(vertex_index) VertexIndex : u32) -> VSOut
    {
        const pos = array<vec2f, 4>(vec2f(-1.0, 1.0), vec2f(-1.0, -1.0), vec2f(1.0, 1.0), vec2f(1.0, -1.0));
        var vsOut: VSOut;
        vsOut.position = vec4f(pos[VertexIndex], 0.0, 1.0);
        vsOut.coords = pos[VertexIndex];
        return vsOut;
    }
    
    const Pi = 3.1415926535; // Pi, but not
    const lightPosition = vec3f(0.0, 1.0, 0.0);
    const epsilon = 0.01;
    const MAX_LEVEL = 20u;
    const BSP_LEAF = 3u;
    var<private> branch_node: array<vec2u, MAX_LEVEL>;
    var<private> branch_ray: array<vec2f, MAX_LEVEL>;
    
    // Mirror sphere
    const MirrorCenter = vec3f(420, 90, 370);
    const MirrorRadius = 90;
    const MirrorShader = 1;

    // Glass sphere
    const GlassCenter = vec3f(130, 90, 250);
    const GlassRadius = 90;
    const GlassShader = 2;
    const GlassRI = 1.5;

    struct Ray 
    {
        origin: vec3f,
        direction: vec3f,
        tmin: f32,
        tmax: f32
    };

    fn intersect_min_max(r: ptr<function, Ray>) -> bool
    {
        let p1 = (aabb.min - (*r).origin)/(*r).direction;
        let p2 = (aabb.max - (*r).origin)/(*r).direction;
        let pmin = min(p1, p2);
        let pmax = max(p1, p2);
        let tmin = max(pmin.x, max(pmin.y, pmin.z));
        let tmax = min(pmax.x, min(pmax.y, pmax.z));
        if (tmin > tmax || tmin > (*r).tmax || tmax < (*r).tmin) 
        {
            return false;
        }
        (*r).tmin = max(tmin - epsilon, (*r).tmin);
        (*r).tmax = min(tmax + epsilon, (*r).tmax);
        return true;
    }

    fn intersect_trimesh(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> bool
    {
        var branch_lvl = 0u;
        var near_node = 0u;
        var far_node = 0u;
        var t = 0.0f;
        var node = 0u;
        for(var i = 0u; i <= MAX_LEVEL; i++) 
        {
            let tree_node = bspTree[node];
            let node_axis_leaf = tree_node.x&3u;
            if (node_axis_leaf == BSP_LEAF) 
            {
                // A leaf was found
                let node_count = tree_node.x>>2u;
                let node_id = tree_node.y;
                var found = false;
                for(var j = 0u; j < node_count; j++) 
                {
                    let obj_idx = treeIds[node_id + j];
                    if(intersect_triangle(*r, hit, obj_idx)) 
                    {
                        (*r).tmax = (*hit).dist;
                        (*hit).has_hit = true;
                        found = true;
                    }
                }
                if(found) { return true; }
                else if(branch_lvl == 0u) { return false; }
                else 
                {
                    branch_lvl--;
                    i = branch_node[branch_lvl].x;
                    node = branch_node[branch_lvl].y;
                    (*r).tmin = branch_ray[branch_lvl].x;
                    (*r).tmax = branch_ray[branch_lvl].y;
                    continue;
                }
            }
            let axis_direction = (*r).direction[node_axis_leaf];
            let axis_origin = (*r).origin[node_axis_leaf];
            if(axis_direction >= 0.0f) 
            {
                near_node = tree_node.z; // left
                far_node = tree_node.w; // right
            }
            else 
            {
                near_node = tree_node.w; // right
                far_node = tree_node.z; // left
            }
            let node_plane = bspPlanes[node];
            let denom = select(axis_direction, 1.0e-8f, abs(axis_direction) < 1.0e-8f);
            t = (node_plane - axis_origin)/denom;
            if(t > (*r).tmax) { node = near_node; }
            else if(t < (*r).tmin) { node = far_node; }
            else 
            {
                branch_node[branch_lvl].x = i;
                branch_node[branch_lvl].y = far_node;
                branch_ray[branch_lvl].x = t;
                branch_ray[branch_lvl].y = (*r).tmax;
                branch_lvl++;
                (*r).tmax = t;
                node = near_node;
            }
        }
        return false;
    }
        
    fn get_camera_ray(uv: vec2f) -> Ray
    {
        const e = vec3f(277.0, 275.0, -570.0);
        const p = vec3f(277.0, 275.0, 0.0);
        const u = vec3f(0.0, 1.0, 0.0);
        var d = uniforms.camera_constant;
        var v = normalize(p-e);
        var b1 = normalize(cross(v, u));
        var b2 = cross(b1, v);
        var q = b1 * uv.x + b2 * uv.y + v * d;
        var omega = normalize(q);
        
        var ray = Ray(e, omega, epsilon, 1.0e32 - epsilon);
        return ray;
    }
    
    struct Light
    {
        L_i: vec3f,
        direction: vec3f,
        dist: f32
    };

    fn sample_point_light(pos: vec3f) -> Light
    {
        var lightIntensity = vec3f(0.0); 
        
        var lightPosition = vec3f(0.0);

        for (var i = 0; i < 2; i++) 
        {
            let temp = faceMat[light_index[i]];
            lightPosition += attribs[temp.x].triangle + attribs[temp.y].triangle + attribs[temp.z].triangle;
            lightIntensity += mEmissionColor[faceMat[light_index[i]].w*2].rgb;
        }
        lightPosition /= 6.0;

        var l = (pos - lightPosition);
        
        var dist = length(l);

        var L_i = vec3f(0.0);
        var area = 0.0;
        var n = vec3f(0.0);
        
        for (var i = 0; i < 2; i++) 
        {
            let temp = faceMat[light_index[i]];
            var e0 = (attribs[temp.y].triangle - attribs[temp.x].triangle);
            var e1 = (attribs[temp.z].triangle - attribs[temp.x].triangle);
            n = cross(e0, e1);
            area += length(n)/2;
        }
        L_i = dot(normalize(l), normalize(n)) * lightIntensity * area;
        
        var light = Light(L_i, normalize(l), dist);
        return light;
    }

    fn sample_directional_light() -> Light 
    {
        const intensityGAINS = 1;
        const lightIntensity = vec3f(Pi*intensityGAINS, Pi*intensityGAINS, Pi*intensityGAINS);

        var L_i = lightIntensity;

        var l = normalize(vec3f(-1.0, -1.0, -1.0));

        var dist = 1e32;

        var light = Light(L_i, l, dist);
        return light;
    }

    struct HitInfo
    {
        has_hit: bool,
        dist: f32,
        position: vec3f,
        normal: vec3f,
        ambientColor: vec3f,
        diffuseColor: vec3f,
        emission: vec3f,
        shader: f32,
        ior1_over_ior2: f32,
        rho_s: f32,
        s: f32,
        material: u32,
    };

    fn intersect_scene(r: ptr<function, Ray>, hit : ptr<function, HitInfo>) -> bool
    {
        if (intersect_sphere(*r, hit, MirrorCenter, MirrorRadius)) {
            (*r).tmax = min((*hit).dist, (*r).tmax);
            (*hit).has_hit = true;
            (*hit).shader = MirrorShader;
        }

        if (intersect_sphere(*r, hit, GlassCenter, GlassRadius)) {
            (*r).tmax = min((*hit).dist, (*r).tmax);
            (*hit).has_hit = true;
            (*hit).shader = GlassShader;
            (*hit).ior1_over_ior2 = GlassRI;
        }

        if (intersect_min_max(r)) 
        {
            if (intersect_trimesh(r, hit)) 
            {
                (*hit).has_hit = true;
                (*hit).shader = 0;
                if ((*r).tmax == (*hit).dist) {
                    (*hit).diffuseColor = mEmissionColor[(*hit).material * 2 + 1].rgb;
                    (*hit).emission = mEmissionColor[(*hit).material * 2].rgb;
                }
            }
            
        }
        return (*hit).has_hit;
    }
    
    fn intersect_triangle(r: Ray, hit: ptr<function, HitInfo>, index: u32) -> bool 
    {
        let temp = faceMat[index];
        var e0 = attribs[temp.y].triangle - attribs[temp.x].triangle;
        var e1 = attribs[temp.z].triangle - attribs[temp.x].triangle;
        var n = cross(e0, e1);

        var dotMan = dot(r.direction, n);
        if (abs(dotMan) < 1e-8) 
        {
            return false;
        }

        var genbrug = attribs[temp.x].triangle-r.origin;
        var t = (dot(genbrug, n)) / dotMan;
        var x = vec3f(0);
        
        var objectHit = false;
        if ((t < r.tmax) & (t > r.tmin)) {
            var beta = dot(cross(genbrug, r.direction), e1)/dotMan;
            var gamma = -dot(cross(genbrug, r.direction), e0)/dotMan;
            var alpha = 1 - beta - gamma;
            objectHit = (beta >= 0) & (gamma >= 0) & ((beta + gamma) <= 1);
            
            if (objectHit) {
                if ((*hit).dist > 0) {
                    (*hit).dist = min((*hit).dist, t);
                }
                else {
                    (*hit).dist = t;
                }
                (*hit).position = r.origin + t * r.direction;
                x = alpha * attribs[temp.x].normal + beta * attribs[temp.y].normal + gamma * attribs[temp.z].normal;
                (*hit).normal = normalize(x);
                (*hit).material = faceMat[index].w;
            }
        }
        return objectHit;
    }

    fn intersect_sphere(r: Ray, hit: ptr<function, HitInfo>, center: vec3f, radius: f32) -> bool 
        {
            var e = r.origin;
            var d = r.direction;
            var disc = (dot(d, (e - center)) * dot(d, (e - center))) - dot(d, d) * (dot((e - center), (e - center)) - (radius*radius));
            var objectHit = (disc >= 0);
            if (objectHit) {
                var t1 = dot(-d, (e - center)) - sqrt(disc) / dot(d, d);
                var t2 = dot(-d, (e - center)) + sqrt(disc) / dot(d, d);
                if ((t1 < r.tmax) && (t1 > r.tmin)) {
                    if ((*hit).dist > 0) {
                        (*hit).dist = min((*hit).dist, t1);
                    }
                    else {
                        (*hit).dist = t1;
                    }
                    (*hit).position = r.origin + (*hit).dist * r.direction;
                    (*hit).normal = normalize((*hit).position - center);
                }
                else if ((t2 < r.tmax) && (t2 > r.tmin)) {
                    if ((*hit).dist > 0) {
                        (*hit).dist = min((*hit).dist, t2);
                    }
                    else {
                        (*hit).dist = t2;
                    }
                    (*hit).position = r.origin + (*hit).dist * r.direction;
                    (*hit).normal = normalize((*hit).position - center);
                }
                else {
                    objectHit = false;
                }
            }
            return objectHit;
        }

    fn lambertian(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
    {       
        var light = sample_point_light((*hit).position);

        var ray = Ray((*hit).position, -light.direction, epsilon, light.dist-epsilon);
        var temp = HitInfo(false, 0.0, vec3f(0.0), vec3f(0.0), vec3f(0.0), vec3f(0.0), vec3f(0.0), 0, 0, 0, 0, 0);
        var result = (*hit).emission;
        if (intersect_scene(&ray, &temp)) { return (vec3f(0.0)); }
        else {
            result += ((*hit).diffuseColor/Pi) * (1 / (light.dist*light.dist)) * dot(-light.direction, (*hit).normal) * light.L_i;
        }
        return result;
    }

    fn mirror(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f 
    {   
        (*r).direction = reflect((*r).direction, (*hit).normal);
        (*r).origin = (*hit).position;
        (*r).tmin = epsilon;
        (*r).tmax = 1e32-epsilon;

        (*hit).has_hit = false;
        
        return vec3f(0,0,0);
    }
    
    fn refraction(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
        {
        if (dot((*r).direction, (*hit).normal) < 0) {
            (*hit).ior1_over_ior2 = 1/(*hit).ior1_over_ior2;
        }
        else {
            (*hit).ior1_over_ior2 = (*hit).ior1_over_ior2/1;
            (*hit).normal = -(*hit).normal;
        }

        var cosSqTheta_t = 1 - pow((*hit).ior1_over_ior2, 2) * (1 - pow(dot((*r).direction, (*hit).normal),2));
        
        if (cosSqTheta_t < 0) {
            return mirror(r, hit);
        }
        else {
            (*r).direction = (*hit).ior1_over_ior2 * (dot(-(*r).direction, (*hit).normal) * (*hit).normal + (*r).direction) - (*hit).normal * sqrt(cosSqTheta_t);
            (*hit).has_hit = false;
            (*r).origin = (*hit).position;
            (*r).tmin = epsilon;
            (*r).tmax = 1e32-epsilon;
            return vec3f(0,0,0);
        }
    }

    fn shade(r: ptr<function, Ray>, hit: ptr<function, HitInfo>) -> vec3f
    {       
        switch i32((*hit).shader) {
            case 1 { return mirror(r, hit); }
            case 2 { return refraction(r, hit); }
            case default { return (lambertian(r, hit)); }
        }
    }
    
    @fragment
    fn main_fs(@location(0) coords: vec2f) -> @location(0) vec4f
    {
        const bgcolor = vec4f(0.1, 0.3, 0.6, 1.0);
        const max_depth = 10;
        var result = vec3f(0.0);
        for (var j = 0u; j < u32(uniforms.jitterSub*uniforms.jitterSub); j++) 
        {
            let uv = vec2f(coords.x*uniforms.aspect*0.5f, coords.y*uniforms.aspect*0.5f);
            var r = get_camera_ray(uv+jitter[j]);
            var hit = HitInfo(false, 0.0, vec3f(0.0), vec3f(0.0), vec3f(0.0), vec3f(0.0), vec3f(0.0), 0, 0, 0, 0, 0);
    
            for(var i = 0; i < max_depth; i++) 
            {
                if(intersect_scene(&r, &hit)) { 
                    result += shade(&r, &hit);
                }
                else { 
                    result += bgcolor.rgb;
                    break; 
                }
    
                if(hit.has_hit) { break; }
            }
        }
        result /= uniforms.jitterSub*uniforms.jitterSub;
        return vec4f(pow(result, vec3f(0.66)), bgcolor.a);
    }
    
    </script>
</head>
<body>
    <canvas id="webgpu-canvas" width="512" height="512">
        Please use a browser that supports HTML5 canvas.
    </canvas>
</body>
</html>